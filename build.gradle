apply from: 'buildsystem/dependencies.gradle'
apply from: 'buildsystem/projectDependencyGraph.gradle'
apply plugin: 'com.github.ben-manes.versions'

static String getVersionName(int major, int minor, int build) {
    String version = "$major.$minor.$build"
    return "$version"
}

ext {
    appName = "BoilerField"
    apiUrl = "https://api.github.com"
    host = '"boilerfield.com"'
    authority = '"https://boilerfield.com"'
    appnav = '"appnav://boilerfield.com"'

    debounceClickMs = "500L"
    debounceInputMs = "200L"
    debounceInputNetworkMs = "500L"
    postDelayMs = "1000L"
}

buildscript {
    ext.kotlin_version = '1.3.50'

    apply from: 'buildsystem/versions.gradle'

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${buildGradleVersion}"
        classpath "com.google.gms:google-services:${googleServicesVersion}"
        if (!project.hasProperty("disable-firebase-performance-plugin")) {
            classpath "com.google.firebase:perf-plugin:${firebasePerformancePluginVersion}"
        }
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${kotlinVersion}"
//        classpath 'io.fabric.tools:gradle:1.+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    ext {
        majorVersion = 1
        minorVersion = 0
        buildVersion = 1

        androidApplicationId = 'com.orcchg.boilerfield'
        androidVersionCode = buildVersion
        androidVersionName = getVersionName(majorVersion, minorVersion, buildVersion)
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = 'com.orcchg.boilerfield.test'
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion

                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                    versionCode rootProject.ext.androidVersionCode
                    versionName rootProject.ext.androidVersionName
                    testInstrumentationRunner rootProject.ext.testInstrumentationRunner
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                sourceSets {
                    main {
                        java.srcDirs += 'src/main/kotlin'
                    }
                    androidTest.java.srcDirs += 'src/androidTest/kotlin'
                    test.java.srcDirs += 'src/test/kotlin'
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
